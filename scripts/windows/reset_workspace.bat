:: This script removes every file generated by premake as well as other IDE-related files

@echo off
setlocal enabledelayedexpansion

for /r %%F in ("*.vcxproj" "*.vcxproj.filters" "*.vcxproj.user" "*.sln" "*.dsp" "*.dsw" "*.cbp" "*.workspace" "*Makefile") do (
    set "filepath=%%F"
    if "!filepath:external\=!" == "!filepath!" (
        del /f /q "%%F" 2>nul
        set "relPath=%%F"
        set "relPath=!relPath:%CD%=!"
        if "!relPath:~0,1!"=="\" set "relPath=!relPath:~1!"
        echo Deleted !relPath!
    )
)

for /d /r %%D in (.vs .vscode .idea .settings .cproject .metadata) do (
    set "folderpath=%%D"
    if "!folderpath:external\=!" == "!folderpath!" (
        if exist "%%D" (
            rmdir /s /q "%%D" 2>nul
            set "relPath=%%D"
            set "relPath=!relPath:%CD%=!"
            if "!relPath:~0,1!"=="\" set "relPath=!relPath:~1!"
            echo Deleted !relPath!
        )
    )
)

echo Workspace reset.
endlocal